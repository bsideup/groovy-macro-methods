buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.4.1'
    }
}

repositories {
    jcenter()
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'ru.trylogic.groovy.macro'
version = '0.2.0'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    testCompile 'junit:junit:4.11'
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "bsideup"
            name "Sergei Egorov"
            email "bsideup@gmail.com"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Macro methods for Groovy')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = bintrayUser ?: 'USER'
    key = bintrayKey ?: 'KEY'
    publications = ['mavenJava']
    pkg {
        userOrg = 'bsideup'
        repo = 'groovy'
        name = 'groovy-macro-methods'
        licenses = ['Apache-2.0']
        websiteUrl = 'https://github.com/bsideup/groovy-macro-methods'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
